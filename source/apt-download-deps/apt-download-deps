#!/usr/bin/env perl
#cito M:755 O:0 G:0 T:/usr/bin/apt-download-deps
#------------------------------------------------------------------------------
# Project Name      - PerlProjects/source/apt-download-deps
# Started On        - Sat  4 Jan 21:55:31 GMT 2020
# Last Change       - Sun 14 Mar 00:05:15 GMT 2021
# Author E-Mail     - terminalforlife@yahoo.com
# Author GitHub     - https://github.com/terminalforlife
#------------------------------------------------------------------------------
# Dependencies:
#
#   apt (>= 1.6.12)
#   libapt-pkg-perl (>= 0.1.29build7)
#   perl (>= 5.26.1)
#------------------------------------------------------------------------------

require AptPkg::Cache;

use v5.22.1;
use strict;
use warnings;
use autodie;

my $CurVer = '2021-03-14';
my $Progrm = 'apt-download-deps';

sub Usage {
	print(qq{Usage: $Progrm [OPTS] [PKG_1 [PKG_2] ...]

		  -h, --help               - Display this help information.
		  -v, --version            - Output the version datestamp.
		  -b, --include-base       - Also download base package(s).
	} =~ tr/\t//dr)
}

my $BaseToo = 0;

while (defined($ARGV[0])) {
	if ($ARGV[0] =~ '^(--help|-h|-\?)$') {
		Usage(1); exit(0)
	} elsif ($ARGV[0] =~ '^(--version|-v)$') {
		print("$CurVer\n"); exit(0)
	} elsif ($ARGV[0] =~ '^(--include-base|-b)$') {
		$BaseToo = 1
	} elsif ($ARGV[0] =~ '^-') {
		die("Incorrect argument(s) specified")
	} else {
		last
	}

	shift()
}

scalar(@ARGV) or die('At least one package is required');

my $Found = 0;
foreach my $Dir (split(':', $ENV{'PATH'})) {
	-d -r -x $Dir or next;

	foreach my $File (glob("$Dir/*")) {
		-r -x $File or next;

		$File =~ '.*/apt-get$' and $Found = 1
	}
}

$Found or die("Dependency 'apt-get' not met");

#----------------------------------------------------Determine the Dependencies

use AptPkg::Cache;

my $Cache = AptPkg::Cache->new();

my $MissingPKG = 0;
sub FetchDeps($) {
	my $Get = $Cache->get($_[0]);
	my $Vers = $Get->{'VersionList'};

	unless (defined($Vers)) {
		warn("Package '$_[0]' not found");
		$MissingPKG = 1;
		return(0)
	}

	my $Deps = $Vers->[0]->{'DependsList'};

	my @Packages;

	my $Iter = 0;
	while ($Iter < scalar(@{$Deps})) {
		my $Dep = $Deps->[$Iter++];

		next unless $Dep->{'DepType'} eq 'Depends';

		push(@Packages, $Dep->{'TargetPkg'}->{'Name'})
	}

	return(@Packages)
}

#---------------------------------------------------Start Download Debian Files

my @ToDL;
while (scalar(@ARGV) > 0) {
	push(@ToDL, FetchDeps($ARGV[0]));
	if ($BaseToo) {
		system("apt-get download $ARGV[0] @ToDL") unless $MissingPKG;
	} else {
		system("apt-get download @ToDL") unless $MissingPKG;
	}

	shift()
}
