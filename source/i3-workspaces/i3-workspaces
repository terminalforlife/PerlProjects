#!/usr/bin/env perl
#cito M:755 O:0 G:0 T:/usr/bin/i3-workspaces
#------------------------------------------------------------------------------
# Project Name      - PerlProjects/source/i3-workspaces/i3-workspaces
# Started On        - Fri 21 Jan 18:01:01 GMT 2022
# Last Change       - Wed  2 Feb 14:46:57 GMT 2022
# Author E-Mail     - terminalforlife@yahoo.com
# Author GitHub     - https://github.com/terminalforlife
#------------------------------------------------------------------------------
# Features:
#
#TODO: Flags `--only-*` don't make much sense with NR when choosing a property.
#
#      Make the use of these flags require only the PROP, not the NR. This
#      wound up being more complicated than I thought, because I didn't write
#      this program with this in mind from the get-go. Needs to be like DTP.
#
#TODO: Consider colorizing True (green) and false (red) values.
#
# Bugs:
#
# N/A
#
# Dependencies:
#
#   libanyevent-i3-perl (>= 0.17-1)
#   perl (>= 5.22.1-9)
#------------------------------------------------------------------------------

require AnyEvent::I3;

use v5.22.1;
use strict;
use warnings;
use AnyEvent::I3 'i3', 'TYPE_GET_WORKSPACES';

no warnings 'uninitialized';

my $CurVer = '2022-02-02';
my $Progrm = 'i3-workspaces';

sub Usage() {
	print(qq{Usage: $Progrm [OPTS] [NR PROP]

		  -h, --help               - Display this help information.
		  -v, --version            - Output the version datestamp.
		  --csv                    - Output in CSV format, for parsing.
		  --only-focused           - Filter out focused workspaces.
		  --only-visible           - Filter out visible workspaces.
		  -C, --no-color           - Disable ANSI color escape sequences.
		  -q, --quotes             - Quote protect NAME and OUTPUT fields.

		  It's also possible to output only property PROP of workspace NR.
		  Valid PROPs are name, visible, output, urgent, and focused.
	} =~ tr/\t//dr)
}

sub Err($;$) {
	say(STDERR "Err: $_[1]");
	exit($_[0]) if $_[0] > 0
}

my @Valids = ('name', 'visible', 'output', 'urgent', 'focused');
my $Grey = "\e[2;37m";
my $Reset = "\e[0m";
my $GetValue = 0;
my $OnlyVisible = 0;
my $OnlyFocused = 0;
my $CSV = 0;

my ($Arg_Quote, $Arg_NoColor, $Quote);

while (defined($ARGV[0])) {
	if ($ARGV[0] =~ '^(--help|-h)$') {
		Usage(); exit(0)
	} elsif ($ARGV[0] =~ '^(--version|-v)$') {
		say("$CurVer"); exit(0)
	} elsif ($ARGV[0] =~ '^(--version|-v)$') {
		say("$CurVer"); exit(0)
	} elsif ($ARGV[0] =~ '^(--no-color|-C)$') {
		$Arg_NoColor = $ARGV[0];
		undef($Grey);
		undef($Reset)
	} elsif ($ARGV[0] =~ '^(--quotes|-q)$') {
		$Arg_Quote = $ARGV[0];
		$Quote = "'"
	} elsif ($ARGV[0] eq '--only-visible') {
		$OnlyVisible = 1
	} elsif ($ARGV[0] eq '--only-focused') {
		$OnlyFocused = 1
	} elsif ($ARGV[0] eq '--csv') {
		$CSV = 1
	} elsif ($ARGV[0] =~ '^-') {
		Err(1, "Incorrect option(s) specified.")
	} else {
		last()
	}

	shift()
}

if (scalar(@ARGV) > 2) {
	for (my $Index = 2; $Index < scalar(@ARGV); $Index++) {
		Err(0, "Argument '$ARGV[$Index]' invalid.")
	}

	exit(1)
} elsif ($Quote and $CSV) {
	Err(1, "Option '--csv' and '$Arg_Quote' are incompatible.")
} elsif (defined($Arg_NoColor) and $CSV) {
	Err(1, "Option '--csv' and '$Arg_NoColor' are incompatible.")
}

my $Arg_Nr = lc($ARGV[0]);
my $Arg_Key = lc($ARGV[1]);
if (defined($ARGV[0]) or defined($ARGV[1])) {
	if (not defined($Arg_Nr) and not defined($Arg_Key)) {
		Err(1, 'Unpaired NR and PROP arguments.')
	} elsif (not grep({$_ eq $ARGV[1]} @Valids)) {
		Err(1, 'Unknown workspace property.')
	} elsif ($CSV) {
		Err(1, "Option '--csv' cannot be used with NR PROP.")
	}

	$GetValue = 1
}

#----------------------------------------------------------------Main Functions

sub Boolean($) {
	if ($_[0] == 0) {
		return("False")
	} elsif ($_[0] == 1) {
		return("True")
	}
}

# Usage: LenChk(STR, MAX_LEN)
sub LenChk($;$) {
	my $Len = length($_[0]);
	if ($Len > $_[1]) {
		return($Len)
	} else {
		return($_[1])
	}
}

#----------------------------------------------------------Fetch Data from JSON

my $i3 = i3();
$i3->connect()->recv() or Err(1, 'Failed to connect to i3-wm.');
my $JSON = $i3->message(TYPE_GET_WORKSPACES)->recv();

my $Nr_LenMax = 2;
my $ID_LenMax = 2;
my $Name_LenMax = 4;
my $Visible_LenMax = 7;
my $Output_LenMax = 6;
my $Urgent_LenMax = 6;
my $Focused_LenMax = 7;

my %Keys;
foreach (@{$JSON}) {
	my $Nr = $_->{num};
	my $Name = "$Quote$_->{name}$Quote";
	my $Output = "$Quote$_->{output}$Quote";
	my $Visible = Boolean($_->{visible});
	my $Urgent = Boolean($_->{urgent});
	my $Focused = Boolean($_->{focused});

	next() if $OnlyVisible and $Visible ne 'True';
	next() if $OnlyFocused and $Focused ne 'True';

	# Determine the correct width of the columns.
	$Nr_LenMax = LenChk($Nr, $Nr_LenMax);
	$Name_LenMax = LenChk($Name, $Name_LenMax);
	$Output_LenMax = LenChk($Output, $Output_LenMax);
	$Visible_LenMax = LenChk($Visible, $Visible_LenMax);
	$Urgent_LenMax = LenChk($Urgent, $Urgent_LenMax);
	$Focused_LenMax = LenChk($Focused, $Focused_LenMax);

	$Keys{$Nr} = {
		'name' => $Name,
		'output' => $Output,
		'visible' => $Visible,
		'urgent' => $Urgent,
		'focused' => $Focused
	}
}

my @SortedKeys = sort({$a <=> $b} keys(%Keys));

#------------------------------------------------------------------Display Data

if ($CSV) {
	say('nr,name,output,visible,urgent,focused');

	foreach (@SortedKeys) {
		printf(
			"%d,%s,%s,%s,%s,%s\n", $_, $Keys{$_}->{name},
			$Keys{$_}->{output}, $Keys{$_}->{visible}, $Keys{$_}->{urgent},
			$Keys{$_}->{focused}
		)
	}
} elsif ($GetValue) {
	if ($Arg_Nr !~ /^[[:digit:]]+$/) {
		Err(1, "Workspace '$Arg_Nr' invalid.")
	} elsif (not grep({$_ eq $Arg_Nr} keys(%Keys))) {
		Err(1, "Workspace #$Arg_Nr not found.")
	}

	say("$Keys{$Arg_Nr}{$Arg_Key}");
} else {
	printf(
		"$Grey%-*s %-*s %-*s %-*s %-*s %*s$Reset\n", $Nr_LenMax, 'NR',
		$Name_LenMax, 'NAME', $Output_LenMax, 'OUTPUT', $Visible_LenMax,
		'VISIBLE', $Urgent_LenMax, 'URGENT', $Focused_LenMax, 'FOCUSED'
	);

	foreach my $Nr (@SortedKeys) {
		printf(
			"%-*d %-*s %-*s %-*s %-*s %*s\n", $Nr_LenMax, $Nr, $Name_LenMax,
			$Keys{$Nr}->{name}, $Output_LenMax, $Keys{$Nr}->{output},
			$Visible_LenMax, $Keys{$Nr}->{visible}, $Urgent_LenMax,
			$Keys{$Nr}->{urgent}, $Focused_LenMax, $Keys{$Nr}->{focused}
		)
	}
}
