#!/usr/bin/env perl
#cito M:755 O:0 G:0 T:/usr/bin/cheater
#------------------------------------------------------------------------------
# Project Name      - PerlProjects/source/cheater/cheater
# Started On        - Sun 22 Nov 20:45:37 GMT 2020
# Last Change       - Sat 16 Apr 22:43:19 BST 2022
# Author E-Mail     - terminalforlife@yahoo.com
# Author GitHub     - https://github.com/terminalforlife
#------------------------------------------------------------------------------
# With the blessing from Igor Chubin, author of 'cheat.sh' and associated
# repositories, I've written an alternative interface to the plentiful source
# of information over at Chubin's GitHub repository 'cheat.sheets'.
#
# If you'd like to add to Cheater, you can do so while also helping 'cheat.sh'.
# Simply submit your 'cheat sheets' to the 'cheat.sheets' repository.
#
# I wanted to use GitHub's API and its handy JSON output, but of course they
# heavily restrict it.
#
# Features:
#
# N/A
#
# Bugs:
#
#TODO: Cannot find any sheet existing sheets on Linux Mint 18.3 with Cinnamon.
#
#      "Failed to access sheet 'CMD' data via GitHub..." Where CMD is the sheet
#      it can't find.
#
# Dependencies:
#
#   libwww-perl (>= 6.31-1)
#   perl (>= 5.26.1)
#   perl-modules-5.26 (>= 5.26.1-6)
#------------------------------------------------------------------------------

use strict;
use warnings;
use LWP::Simple;
use Term::ANSIColor 'color';

no warnings 'uninitialized';

my $CurVer = '2022-04-16';
my $Progrm = 'cheater';

sub Usage {
	print(qq{Usage: $Progrm [OPTS] [SHEET_1 [SHEET_2] ...]

		  -h, --help               - Display this help information.
		  -v, --version            - Output the version datestamp.
		  -C, --no-colors          - Disable ANSI color escape sequences.
		  -H, --highlight REGEX    - Highlight REGEX in the requested sheet(s).
		  -P, --no-pager           - Do not use less(1) or more(1) for paging.
	} =~ tr/\t//dr)
}

my $ShareDir = "/usr/share/$Progrm";
my $ExcludedFile = "$ShareDir/excluded_lines";

my $DirURL = 'https://github.com/chubin/cheat.sheets/tree/master/sheets';
my $RawURL = 'https://raw.githubusercontent.com/chubin/cheat.sheets/master/sheets';

my $Pad = ' ' x 4;
my ($NoPager, $PagerFound, %Colors, @AvailFiles, $Highlight, $NoColors);

while (defined($ARGV[0])) {
	if ($ARGV[0] =~ '^(--help|-h)$') {
		Usage(); exit(0)
	} elsif ($ARGV[0] =~ '^(--version|-v)$') {
		print("$CurVer\n"); exit(0)
	} elsif ($ARGV[0] =~ '^(--no-color|-C)$') {
		$NoColors = 1
	} elsif ($ARGV[0] =~ '^(--no-pager|-P)$') {
		$NoPager = 1
	} elsif ($ARGV[0] =~ '^(--highlight|-H)$') {
		shift();

		if (length($ARGV[0]) == 0) {
			die("Missing REGEX")
		} else {
			$Highlight = $ARGV[0]
		}
	} elsif ($ARGV[0] =~ '^-') {
		die("Incorrect option(s) specified")
	} else {
		last
	}

	shift()
}

scalar(@ARGV) or die('Missing COMMAND to query');
-d $ShareDir or die("Directory '$ShareDir' missing");

%Colors = (
	'desc' => color('bright_black'),
	'match' => color('bright_red'),
	'reset' => color('reset')
) unless $NoColors;

# Check for less(1) and/or more(1) in PATH, if paging is enabled.
unless ($NoPager) {
	my @Executables;
	foreach my $Dir (split(':', $ENV{'PATH'})) {
		-d -r -x $Dir or next;

		foreach my $File (glob("$Dir/*")) {
			-f -r -x $File and push(@Executables, $File =~ m{.*/(.*)$})
		}
	}

	# Set the pager which is found, using more(1) as a fallback.
	grep({$_ eq 'more'} @Executables) and $PagerFound = 'more';
	grep({$_ eq 'less'} @Executables) and $PagerFound = 'less -R 2>&1';
}

# If we have to use more(1), call `die()` if using colors; unsupported.
($PagerFound eq 'more' and not $NoColors)
	and die('Color not supported in more(1)');

# We cannot use highlighting while disabling colors.
if (length($Highlight) and $NoColors) {
	die('Highlighting unavailable while colors are disabled')
}

#----------------------------------------------Populate List of Lines to Ignore

my @Excludes;
if (-f $ExcludedFile) {
	-r $ExcludedFile or die("File '$ExcludedFile' unreadable");

	open(my $FH, '<', $ExcludedFile) or die("$!");

	while (<$FH>) {
		chomp();

		push(@Excludes, $_) unless (/^$/ or /^#/)
	}

	close($FH);
} else {
	die("File '$ExcludedFile' missing")
}

#----------------------------------------------------------Display the Sheet(s)

my ($ComStrip, @FinalLines);
foreach my $CurSheet (@ARGV) {
	my $HeaderSeen = 'False';

	my $Response;
	if ($CurSheet =~ '/') {
		# Using one of the '_*' subdirectories.
		my ($Left, $Right) = split('/', $CurSheet);
		$Response = get("$RawURL/_$Left/$Right")
	} else {
		$Response = get("$RawURL/$CurSheet")
	}

	if (length($Response)) {
		foreach my $Line (split("\n", $Response)) {
			# This is of no consequence to us, as we're working differently.
			# This is a special line talked about between Chubin and
			# contributors, such as myself, which would contain parameters much
			# like Vim's modeline.
			$Line =~ /^# cheat.sh: / and next;

			# Ignore the first 'block' of comments, since we already know what
			# we chose. The short description would be nice to include, but
			# right now the database isn't ready for such an assumption.
			if ($HeaderSeen eq 'True') {
				if ($Line =~ /^# /) {
					unless ($NoColors) {
						$ComStrip = $Line =~ s/^#\s*//r
					} else {
						# Using this same variable just because I'm being lazy.
						$ComStrip = $Line
					}

					# Some lines have to be ignored by Cheater, because they
					# aren't relevant to the way we're using the sheets.
					grep({$Line =~ /$_/} @Excludes) and next;

					# One here...
					if (length($Highlight) and $Line =~ /$Highlight/) {
						my $Replace = "$Colors{match}$&$Colors{desc}";
						$ComStrip = $ComStrip =~ s/$&/$Replace/rg
					}

					push(@FinalLines, "$Colors{desc}$ComStrip$Colors{reset}\n")
				} elsif ($Line =~ /^#$/) {
					unless ($NoColors) {
						push(@FinalLines, "\n")
					} else {
						# Otherwise it might be a bit confusing.
						push(@FinalLines, "#\n")
					}
				} else {
					my $ElseLine = $Line;

					# ...and one here, to ensure we restore the right color.
					if (length($Highlight) and $ElseLine =~ /$Highlight/) {
						my $Replace = "$Colors{match}$&$Colors{reset}";
						$ElseLine = $ElseLine =~ s/$&/$Replace/rg
					}

					push(@FinalLines, "$ElseLine\n")
				}
			} else {
				if ($Line =~ /^# /) {
					next
				} elsif ($Line =~ /^$/) {
					$HeaderSeen = 'True'
				}
			}
		}

		# Ensures a newline between sheets.
		scalar(@ARGV) and push(@FinalLines, "\n")
	} else {
		die("Failed to access sheet '$CurSheet' data via GitHub")
	}
}

#------------------------------------Begin Displaying Lines & Remove Duplicates

unless ($NoPager) {
	open(my $PH, "| $PagerFound") or die("$!");

	my $OldLine;
	foreach my $NewLine (@FinalLines) {
		$NewLine ne $OldLine and print($PH $NewLine);
		$OldLine = $NewLine
	}

	close($PH)
} else {
	my $OldLine;
	foreach my $NewLine (@FinalLines) {
		$NewLine ne $OldLine and print($NewLine);
		$OldLine = $NewLine
	}
}
