#!/usr/bin/env perl
#cito M:755 O:0 G:0 T:/usr/bin/ldmod
#------------------------------------------------------------------------------
# Project Name      - PerlProjects/source/ldmod
# Started On        - Sat 11 Jan 14:33:58 GMT 2020
# Last Change       - Mon  1 Feb 01:42:56 GMT 2021
# Author E-Mail     - terminalforlife@yahoo.com
# Author GitHub     - https://github.com/terminalforlife
#------------------------------------------------------------------------------
# Perl rewrite of and expansion on an old TFL shell function called `lsmodd()`.
# LDMOD stands for 'List and Describe Modules'.
#
# Dependencies:
#
#   kmod (>= 22-1)
#   perl (>= 5.22.1-9)
#------------------------------------------------------------------------------

use strict;
use warnings;
use autodie;

no warnings 'uninitialized';

my $CurVer = '2020-11-18';
my $Progrm = 'ldmod';

sub Usage {
	print(qq{Usage: $Progrm [OPTS] [REGEX]

		  -h, --help               - Display this help information.
		  -v, --version            - Output the version datestamp.
		  -a, --show-all           - Don't omit undescribed modules.
		  -i, --ignore-case        - Case-insensitive REGEX search.

		  Where REGEX is a regular expression string to use in order
		  to filter modules by their names, not their descriptions.
	} =~ tr/\t//dr)
}

my $ProcMod = '/proc/modules';
my ($DoCase, $ShowAll, $LiveOnly);

while (defined($ARGV[0])) {
	if ($ARGV[0] =~ '^(--help|-h|-\?)$') {
		Usage(); exit(0)
	} elsif ($ARGV[0] =~ '^(--version|-v)$') {
		print("$CurVer\n"); exit(0)
	} elsif ($ARGV[0] =~ '^(--ignore-case|-i)$') {
		$DoCase = 'i'
	} elsif ($ARGV[0] =~ '^(--show-all|-a)$') {
		$ShowAll = 'true'
	} elsif ($ARGV[0] =~ '^-') {
		die("Incorrect option(s) specified")
	} else {
		last
	}

	shift()
}

my $RegexStr = $ARGV[0] if defined($ARGV[0]);

if (scalar(@ARGV) > 1) {
	die("Too many arguments -- see: $Progrm -h")
} elsif ($DoCase eq 'i' and scalar(@ARGV) == 0) {
	die("Option '--ignore-case|-i' used, but no REGEX provided.")
} elsif (not -f -r $ProcMod) {
	die(1, "File '$ProcMod' missing or inaccessible.")
}

sub FetchMods {
	open(my $FH, '<', $_[0]);

	my @Mods;
	while (<$FH>) {
		chomp(my @LineArr = split(' ', $_));

		push(@Mods, $LineArr[0])
	}

	close($FH);

	return(@Mods);
}

foreach (sort(FetchMods($ProcMod))) {
	# Look for user-provided REGEX in line, if one is given.
	if (length($RegexStr) > 0) {
		if ($DoCase eq 'i') {
			next unless grep(m/$RegexStr/i, $_)
		} else {
			next unless grep(m/$RegexStr/, $_)
		}
	}

	# This is hopefully just a temporary measure.
	chomp(my $Desc = `modinfo -d "$_" 2> /dev/null` || '?');

	if ($ShowAll eq 'true') {
		printf("%s - %s\n", $_, $Desc)
	} else {
		printf("%s - %s\n", $_, $Desc) unless $Desc eq '?'
	}
}
