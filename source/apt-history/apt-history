#!/usr/bin/env perl
#cito M:755 O:0 G:0 T:/usr/bin/apt-history
#------------------------------------------------------------------------------
# Project Name      - PerlProjects/source/apt-history
# Started On        - Sat 21 Nov 16:11:20 GMT 2020
# Last Change       - Thu 26 Nov 18:03:56 GMT 2020
# Author E-Mail     - terminalforlife@yahoo.com
# Author GitHub     - https://github.com/terminalforlife
#------------------------------------------------------------------------------
# Features:
#
#TODO: Allow apt-history to use X-number of older log files, using GZIP.
#TODO: Provide package listing output suitable for piping.
#TODO: When filtering for users, have option to show only 'System ...' entries.
#
# Bugs:
#
#TODO: Account for filting by both UID and User simultaneously.
#
# Dependencies:
#
#   libterm-readkey-perl (>= 2.33-1build1)
#   libtext-wrapi18n-perl (>= 0.06-7.1)
#   perl (>= 5.22.1-9)
#------------------------------------------------------------------------------

use strict;
use warnings;
use autodie;

no warnings 'uninitialized';

my $CurVer = '2020-11-26';
my $Progrm = ($0 =~ m{(?:.*/)?([^/].*)})[0];

sub Usage {
	print(qq{Usage: $Progrm [OPTS]

		  -h, --help               - Display this help information.
		  -v, --version            - Output the version datestamp.
		  --no-color-status        - Don't show packages not installed in red.
		  -A, --action [TYPE]      - Filter action by TYPE. See below.
		  -C, --no-color           - Disable ANSII color escape sequences.
		  -D, --display [STYLE]    - Display packages in STYLE. See below.
		  -H, --history [FILE]     - Use an alternative 'history.log' file.
		  -U, --user-id [UID]      - Filter the logged results by UID.
		  -c, --command            - Also show the actual command executed.
		  -p, --package [PKG]      - Show results affecting only PKG.
		  -r, --reverse            - Display history in reverse; oldest first.
		  -u, --user [USER]        - Filter the logged results by USER.

		  Available package listing STYLEs are 'OneLine' and 'Wrapped'.
		  Available TYPEs are 'Install', 'Purge', 'Remove', and 'Upgrade'.

		  If color is enabled, packages in red are no longer installed.
	} =~ tr/\t//dr)
}

my $Pad = ' ' x 4;
my $PKGStyle = 'OneLine';
my $HistFile = '/var/log/apt/history.log';
my $StatusFile = '/var/lib/dpkg/status';

my (
	%History, $Date, $Time, $Command, $ReqByUser, $ReqByUID, $Action,
	$Packages, $DoReverse, $ActionString, $DoCommand, $NoColor, $C_Command,
	$C_Arch, $C_Reset, $C_FancyDateTime, $C_Action, $OnlyAction, $FilterUser,
	$FilterUID, $FilterPKG, $C_Name, $NoColorStatus
);

while (defined($ARGV[0])) {
	if ($ARGV[0] =~ '^(--help|-h)$') {
		Usage(); exit(0)
	} elsif ($ARGV[0] =~ '^(--version|-v)$') {
		print("$CurVer\n"); exit(0)
	} elsif ($ARGV[0] =~ '^(--history|-H)$') {
		shift();

		if (length($ARGV[0]) == 0) {
			die("Alternative 'history.log' FILE not provided")
		} else {
			$HistFile = $ARGV[0]
		}
	} elsif ($ARGV[0] =~ '^(--package|-p)$') {
		shift();

		if (length($ARGV[0]) == 0) {
			die("Filter package PKG not provided")
		} else {
			$FilterPKG = $ARGV[0]
		}
	} elsif ($ARGV[0] =~ '^(--display|-D)$') {
		shift();

		if (length($ARGV[0]) == 0) {
			die("Package display STYLE not provided")
		} elsif ($ARGV[0] !~ '^(OneLine|Wrapped)$') {
			die("Invalid package display STYLE provided")
		} else {
			$PKGStyle = $ARGV[0]
		}
	} elsif ($ARGV[0] =~ '^(--action|-A)$') {
		shift();

		if (length($ARGV[0]) == 0) {
			die("No execution action TYPE provided")
		} elsif ($ARGV[0] !~ '^(Install|Remove|Upgrade|Purge)$') {
			die("Invalid execution action TYPE provided")
		} else {
			$OnlyAction = $ARGV[0]
		}
	} elsif ($ARGV[0] =~ '^(--user|-u)$') {
		shift();

		if (length($ARGV[0]) == 0) {
			die("No USER specified for filtering")
		} else {
			$FilterUser = $ARGV[0]
		}
	} elsif ($ARGV[0] =~ '^(--user-id|-U)$') {
		shift();

		if (length($ARGV[0]) == 0) {
			die("No UID specified for filtering")
		} elsif ($ARGV[0] !~ '^[0-9]*$') {
			die("Invalid UID provided")
		} else {
			$FilterUID = $ARGV[0]
		}
	} elsif ($ARGV[0] =~ '^(--reverse|-r)$') {
		$DoReverse = 'True'
	} elsif ($ARGV[0] =~ '^--no-color-status$') {
		$NoColorStatus = 'True'
	} elsif ($ARGV[0] =~ '^(--command|-c)$') {
		$DoCommand = 'True'
	} elsif ($ARGV[0] =~ '^(--no-color|-C)$') {
		$NoColor = 'True'
	} elsif ($ARGV[0] =~ '^-') {
		die("Incorrect option(s) specified")
	} else {
		last
	}

	shift()
}

#---------------------------------------------------Gather Data & Populate Hash

-f $HistFile or die("File '$HistFile' not found");
-r $HistFile or die("File '$HistFile' unreadable");

if (open(my $FH, '<', $HistFile)) {
	while (<$FH>) {
		chomp();

		if (/^Start-Date:/) {
			($Date, $Time) = (split(' '))[1..2]
		} elsif (/^Commandline:/) {
			$Command = (m{(?:$& )(.*)})[0]
		} elsif (/^Requested-By:/) {
			my $ReqBy = (m{(?:$&)(.*)})[0];
			($ReqByUser, $ReqByUID) = (split(' ', $ReqBy))[0..1];
		} elsif (/^(Install|Purge|Remove|Upgrade):/) {
			($Action, $Packages) = (m{(^$&)(.*)})[0..1];

			# We're finished gathering data, so populate hash, now.
			$History{"${Date}_$Time"} = {
				'Date' => $Date,
				'Time' => $Time,
				'Command' => $Command,
				'ReqByUser' => $ReqByUser,
				'ReqByUID' => $ReqByUID =~ tr/()//dr,
				'Action' => $Action =~ tr/://dr,
				'Packages' => $Packages
			}
		}
	}

	close($FH);
}

#----------------------------------Gather Array of Currently Installed Packages

# I could get something like this done via the APT perl module, but that's yet
# another dependency, so I thought I'd just go the pure-Perl route.

-f $StatusFile or die("File '$StatusFile' not found");
-r $StatusFile or die("File '$StatusFile' unreadable");

my (@InstalledPKGs, $InstalledPKG, $InstalledArch, $PKGStatus);
if (open(my $FH, '<', $StatusFile)) {
	while (<$FH>) {
		chomp();

		if (/^Package:/) {
			$InstalledPKG = (split(' '))[1]
		} elsif (/^Status:/) {
			$PKGStatus = $_ =~ s/^Status: //r
		} elsif (/^Architecture:/) {
			$InstalledArch = (split(' '))[1];
			$InstalledPKG = "$InstalledPKG:$InstalledArch";

			if ($PKGStatus eq 'install ok installed') {
				push(@InstalledPKGs, $InstalledPKG)
			}
		}
	}

	close($FH);
}

#-------------------------------------------------------------Use Gathered Data

use Term::ANSIColor 'colorstrip', 'color';
use Term::ReadKey 'GetTerminalSize';
use Text::Wrap 'wrap';
use Time::Piece;

unless ($NoColor eq 'True') {
	$C_Action = color('bright_yellow');
	$C_Arch = color('bright_black');
	$C_Command = color('bright_black');
	$C_FancyDateTime = color('bright_green');
	$C_Name = color('bright_red');
	$C_Reset = color('reset')
}

my @SortedKeys;
if ($DoReverse eq 'True') {
	@SortedKeys = sort({$a cmp $b} keys(%History))
} else {
	@SortedKeys = sort({$b cmp $a} keys(%History))
}

# This is normal, when the logs rotate, but the user might think something was
# wrong; came across this myself and was really confused!
if (scalar(@SortedKeys) == 0) {
	print(STDERR "No APT log entries found -- nothing to do.\n");
	exit(0) # <-- Cleaner; no need to keep going if we're here.
}

$Text::Wrap::columns = (GetTerminalSize())[0];

my $Count = 0;
foreach my $Key (@SortedKeys) {
	my $FancyDateTime = Time::Piece->strptime(
		"$History{$Key}{'Date'} $History{$Key}{'Time'}", '%Y-%m-%d %H:%M:%S'
		#     Using just '%X' doesn't seem to work in Pop! OS 20.04. --^
	);

	if (length($FilterUser) > 0) {
		$FilterUser eq $History{$Key}{'ReqByUser'} or next
	} elsif (length($FilterUID) > 0) {
		$FilterUID eq $History{$Key}{'ReqByUID'} or next
	}

	if (length($OnlyAction) > 0) {
		$OnlyAction eq $History{$Key}{'Action'} or next
	}

	# For pretty-printing the correct verb.
	if ($History{$Key}{'Action'} eq 'Install') {
		$ActionString = "${C_Action}installed$C_Reset"
	} elsif ($History{$Key}{'Action'} eq 'Remove') {
		$ActionString = "${C_Action}removed$C_Reset"
	} elsif ($History{$Key}{'Action'} eq 'Purge') {
		$ActionString = "${C_Action}purged$C_Reset"
	} elsif ($History{$Key}{'Action'} eq 'Upgrade') {
		$ActionString = "${C_Action}upgraded$C_Reset"
	}

	# Populate array with just the current package names.
	my @Packages;
	foreach my $PKG (sort({$a cmp $b} split(' ', $History{$Key}{'Packages'}))) {
		unless ($PKG =~ /[\(\),]/) {
			my ($Name, $Arch) = split(':', $PKG);

			# If package is not currently installed, change its color to red.
			unless ($NoColor eq 'True' or $NoColorStatus eq 'True') {
				unless (grep({$_ eq $PKG} @InstalledPKGs)) {
					$Name = "$C_Name$Name$C_Reset"
				}
			}

			$Arch = "$C_Arch:$Arch$C_Reset";
			push(@Packages, "$Name$Arch")
		}
	}

	if (length($FilterPKG) > 0) {
		# Need to strip ANSI escape sequences otherwise grep() is borked.
		grep({colorstrip($_) eq $FilterPKG} @Packages) or next
	}

	#---------------------------------------------------------Begin Main Output

	# Once filtering is all done, see how many entries were found. We just need
	# this, to discover if nothing was found under the filters used or not.
	$Count++;

	print("$C_FancyDateTime$FancyDateTime$C_Reset\n\n");

	if ($DoCommand eq 'True') {
		# To denote that it's a newline, as it would be on the command-line.
		$Text::Wrap::separator = " \\\n";

		printf(
			"%s\n", wrap($Pad, $Pad,
				"$C_Command$History{$Key}{Command}$C_Reset\n\n"
			)
		);

		# Restore it to a standard newline, for the rest of apt-history.
		$Text::Wrap::separator = "\n"
	}

	unless (length($History{$Key}{'ReqByUser'}) == 0) {
		printf(
			"%sUser '%s' (%d) %s the following:\n", $Pad,
			$History{$Key}{'ReqByUser'},
			$History{$Key}{'ReqByUID'},
			$ActionString
		)
	} else {
		printf("%sSystem %s the following:\n", $Pad, $ActionString);
	}

	print("\n");
	if ($PKGStyle eq 'OneLine') {
		print("$Pad$_\n") foreach (@Packages)
	} elsif ($PKGStyle eq 'Wrapped') {
		print(wrap("$Pad", "$Pad", join(' ', @Packages)) . "\n")
	}

	print("\n")
}

$Count == 0 and print(STDERR "No APT log entries found with given filters.\n");
