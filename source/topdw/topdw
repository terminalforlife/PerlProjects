#!/usr/bin/env perl
#cito M:755 O:0 G:0 T:/usr/local/bin/topdw
#------------------------------------------------------------------------------
# Project Name      - PerlProjects/source/topdw/topdw
# Started On        - Wed  7 Jul 14:59:41 BST 2021
# Last Change       - Fri  2 Jun 18:42:21 BST 2023
# Author E-Mail     - terminalforlife@yahoo.com
# Author GitHub     - https://github.com/terminalforlife
#------------------------------------------------------------------------------
# Simple (at least for now) PERL tool to list DistroWatch's top 10 rated Linux
# distributions, by hits-per-day for the last six months. Basic functionality
# to start with.
#
# Features:
#
#TODO: Add flag to filter results by a given distribution.
#
# Bugs:
#
# N/A
#
# Dependencies:
#
#   libwww-perl (>= 6.31-1)
#   perl (>= 5.26.1)
#------------------------------------------------------------------------------

use strict;
use warnings;
use LWP::UserAgent;

my $CurVer = '2023-06-02';
my $Progrm = ($0 =~ m{(?:.*/)?([^/].*)})[0];

sub Usage {
	print(qq{Usage: $Progrm [OPTS]

		  -h, --help               - Display this help information.
		  -v, --version            - Output the version datestamp.
		  -C, --no-color           - Disable ANSI color escape sequences.
		  -n, --number [INT]       - List up to INT number of distributions.
		  -t, --total              - Display the total number of hits.

		Numbers shown are for the HPD in the Last 6 Months.
	} =~ tr/\t//dr)
}

sub Err($;$) {
    printf(STDERR "ERROR: %s\n", $_[1]);
    $_[0] > 0 and exit($_[0])
}

my $Domain = 'https://distrowatch.com';

my $Green = "\e[92m";
my $Grey = "\e[37m";
my $Red = "\e[91m";
my $Yellow = "\e[93m";
my $Reset = "\e[0m";

my @Args;
while (defined($ARGV[0])) {
	if ($ARGV[0] =~ /^-[^-]/) {
		my $Str = substr($ARGV[0], 1);
		my $Len = length($Str);
		for (my $Index = 0; $Index < $Len; $Index++) {
			push(@Args, '-' . substr($Str, $Index, 1))
		}
	} else {
		push(@Args, $ARGV[0])
	}

	shift()
}

@ARGV = @Args;

my $Color = 1;
my $Total = 0;
my $TargetRank = 10;
while (defined($ARGV[0])) {
	if ($ARGV[0] =~ '^(--help|-h)$') {
		Usage(); exit(0)
	} elsif ($ARGV[0] =~ '^(--version|-v)$') {
		print("$CurVer\n"); exit(0)
	} elsif ($ARGV[0] =~ '^(--no-color|-C)$') {
		$Color = 0;

		$Green = '';
		$Grey = '';
		$Red = '';
		$Yellow = '';
		$Reset = '';
	} elsif ($ARGV[0] =~ '^(--total|-t)$') {
		$Total = 1
	} elsif ($ARGV[0] =~ '^(--number|-n)$') {
		shift();

		my $Option = $1;
		if ($ARGV[0] =~ '^[[:digit:]]+$') {
			if ($ARGV[0] > 100 or $ARGV[0] < 1) {
				Err(1, "OPT '$Option' requires an integer of 1-100.")
			} else {
				$TargetRank = $ARGV[0]
			}
		} else {
			Err(1, "OPT '$1' requires an integer.")
		}
	} else {
		Err(1, 'Incorrect option(s) specified.')
	}

	shift()
}

#----------------------------------------------------------Fetch and Parse Data

my $UA = LWP::UserAgent->new(
	'ua' => 'Mozilla/5.0',
	'protocols_allowed' => ['http', 'https'],
	'max_redirect' => 3,
	'timeout' => 5
);

my $Response = $UA->get($Domain);
$Response->is_success() or Err(1, 'Failed to download web page.');

my $HitsTTL = 0;
my $HitsLenMax = 0;
my $RankLenMax = 0;
my ($HitsLen, $Rank, $RankLen, $Name, $Hits, %Entries);
foreach (split("\n", $Response->decoded_content())) {
	chomp();

	if (/"phr1">(\d+)</) {
		$Rank = $1;

		$RankLen = length($Rank);
		$RankLen > $RankLenMax and $RankLenMax = $RankLen;

		$Rank > $TargetRank and last()
	} elsif (/"phr2"><a href=".*">(.*)<\/a>/) {
		$Name = $1
	} elsif (/"phr3" title="Yesterday: (\d+)">(\d+)</) {
		$Entries{$Rank} = [$Name, $2, $1];
		$HitsTTL += $1
	}
}

$Rank = '';

#------------------------------------------------------------------Display Data

my @Keys = keys(%Entries);

unless (scalar(@Keys)) {
	print(STDERR "Uh-oh! No rankings found.\n");
	exit(1)
}

my $UpDownColor = '';
my $PercentLenMax = 0;
foreach (sort({$a <=> $b} @Keys)) {
	$_ > $TargetRank and last();

	$HitsLen = length($Entries{$_}->[1]);
	$HitsLen > $HitsLenMax and $HitsLenMax = $HitsLen;

	my $UpDown = '?';
	if ($Entries{$_}->[1] > $Entries{$_}->[2]) {
		$Color and $UpDownColor = "\e[1;92m";
		$UpDown = '>'
	} elsif ($Entries{$_}->[1] < $Entries{$_}->[2]) {
		$Color and $UpDownColor = "\e[1;91m";
		$UpDown = '<'
	} elsif ($Entries{$_}->[1] == $Entries{$_}->[2]) {
		$Color and $UpDownColor = "\e[1;93m";
		$UpDown = '='
	}

	my $Percent = sprintf("%3.2f%%", ($Entries{$_}->[1] / $HitsTTL) * 100);

	my $PercentLen = length($Percent);
	$PercentLen > $PercentLenMax and $PercentLenMax = $PercentLen;

	printf(
		"%#*d " . $Grey . "%*d %*s" . $UpDownColor . " %s" . $Reset .
		" %s\n", $RankLenMax, $_, $HitsLenMax, $Entries{$_}->[1],
		$PercentLenMax, $Percent, $UpDown, $Entries{$_}->[0]
	)
}

print("\nTotal: $HitsTTL\n") if $Total
