#!/usr/bin/env perl
#cito M:755 O:0 G:0 T:/usr/bin/redshifter
#------------------------------------------------------------------------------
# Project Name      - PerlProjects/source/redshifter
# Started On        - Fri 19 Apr 23:05:28 BST 2019
# Last Change       - Tue 12 Jan 01:20:58 GMT 2021
# Author E-Mail     - terminalforlife@yahoo.com
# Author GitHub     - https://github.com/terminalforlife
#------------------------------------------------------------------------------
# Dependencies:
#
#   perl (>= 5.22.1-9)
#   redshift (>= 1.11)
#------------------------------------------------------------------------------

use strict;
use warnings;
use autodie;

no warnings 'uninitialized';

my $CurVer = "2021-01-12";
my $Progrm = 'redshifter';

sub Usage{
	print(qq{Usage: $Progrm [OPTS] [ACTION]

		  -h, --help               - Display this help information.
		  -v, --version            - Output the version datestamp.
		  -f, --file PATH          - Use PATH instead of the default.

		  temp	                   - Print the current temperature.
		  lower [N]                - Lower the current gamma by N.
		  raise [N]                - Raise the current gamma by N.
		  reset                    - Reset to the default of 6500.
		  set [N]                  - Manually set the gamma to N.

		  Valid temperatures are between 1000K (warm) and 25000K (cool).
	} =~ tr/\t//dr)
}

my ($Temp, $GammasNow);
my $Buffer = "$ENV{HOME}/.config/rs-gamma";
my $ConfigDir = ($Buffer =~ m{(.*)?(?:/.*)})[0];

scalar(@ARGV) == 0 and die("Argument(s) required");

while (defined($ARGV[0])) {
	if ($ARGV[0] =~ '^(--help|-h|-\?)$') {
		Usage(1); exit(0)
	} elsif ($ARGV[0] =~ '^(--version|-v)$') {
		print("$CurVer\n"); exit(0)
	} elsif ($ARGV[0] =~ '^(--file|-f)$') {
		shift();

		-d $ARGV[0] and die("Option '--file|-f' given a directory");

		$Buffer = $ARGV[0]
	} elsif ($ARGV[0] =~ '^-') {
		die("Incorrect option(s) specified")
	} else {
		last
	}

	shift(@ARGV)
}

my @Bins;
foreach my $Dir (split(':', $ENV{PATH})) {
	-d $Dir or next;
	opendir(my $DH, $Dir);
	push(@Bins, readdir($DH));
	closedir($DH)
}

grep({$_ eq 'redshift'} @Bins) or die("Dependency 'redshift' not found");

# Usage: {[<] | [>]} [GAMMA_INT]
sub GetSetValue {
	if (-f $Buffer) {
		-r $Buffer or die("File '$Buffer' lacks read access");
		-w $Buffer or die("File '$Buffer' lacks write access");
	}

	open(my $FH, $_[0], $Buffer);

	print($FH "$_[1]\n") if $_[0] eq '>';
	chomp($GammasNow = <$FH>) if $_[0] eq '<';

	close($FH)
}

sub ChkVal { # Usage: [ACTION] [VALUE]
	$_[1] =~ '^[0-9]+$' or die("Action '$_[0]' needs an integer");

	if ($_[0] eq 'raise') {
		$Temp = $GammasNow + $_[1];
		if ($Temp > 25000) {
			$Temp = 25000;
			warn('Maximum gamma available is 25000')
		}
	} elsif ($_[0] eq 'lower') {
		$Temp = $GammasNow - $_[1];
		if ($Temp < 1000) {
			$Temp = 1000;
			warn('Minimum gamma available is 1000')
		}
	}
}

# Just in-case the user doesn't yet have a `.config` directory.
-d $ConfigDir or mkdir($ConfigDir);

if (-f $Buffer) {
	GetSetValue('<')
}else{
	GetSetValue('>', 6500)
}

# Parse ACTIONS.
while (defined($ARGV[0])){
	if ($ARGV[0] eq 'set') {
		shift();

		ChkVal('set', $ARGV[0]);
		GetSetValue('>', $ARGV[0]);

		system("redshift -o -O $ARGV[0]K 1> /dev/null")
	} elsif ($ARGV[0] eq 'reset') {
		GetSetValue('>', '6500');

		system('redshift -o -O 6500K 1> /dev/null')
	} elsif ($ARGV[0] eq 'raise') {
		shift();

		ChkVal('raise', $ARGV[0]);
		GetSetValue('>', $Temp);

		system("redshift -o -O ${Temp}K 1> /dev/null")
	} elsif ($ARGV[0] eq 'lower') {
		shift();

		ChkVal('lower', $ARGV[0]);
		GetSetValue('>', $Temp);

		system("redshift -o -O ${Temp}K 1> /dev/null")
	} elsif ($ARGV[0] eq 'temp') {
                print("$GammasNow\n");
	} else {
		die("Incorrect action(s) specified")
	}

	shift(@ARGV)
}
