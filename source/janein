#!/usr/bin/perl
#cito M:755 O:0 G:0 T:/usr/bin/janein
#------------------------------------------------------------------------------
# Project Name      - PerlProjects/source/janein
# Started On        - Wed 28 Oct 17:26:51 GMT 2020
# Last Change       - Wed 13 Apr 16:21:35 BST 2022
# Author E-Mail     - terminalforlife@yahoo.com
# Author GitHub     - https://github.com/terminalforlife
#------------------------------------------------------------------------------
# Programmable Yes/No GTK2 dialog for use in scripts.
#
# Test Results:
#
#   Linux Mint 18.3 (Cinnamon)             | PERFECT
#   Pop! OS 20.04 (GNOME3)                 | BAD
#   Ubuntu 18.04 (i3-wm)                   | PERFECT
#
# Features:
#
#TODO: Add ability to change or supplement title bar.
#TODO: Use a hash for scheme testing/returning.
#TODO: Add error message for too-long a message.
#TODO: Add optional line-wrapping functionality, with correct alignment.
#
# Bugs:
#
#TODO: Handle out-of-range (non-existent) scheme error.
#
# Dependencies:
#
#   libglib-perl (>= 3:1.326-1build1)
#   libgtk2-perl (>= 2:1.24992-1build1)
#   perl (>= 5.26.1-6)
#------------------------------------------------------------------------------

use strict;
use warnings;
use autodie;
use Glib 'TRUE', 'FALSE';
use Gtk2 '-init';

no warnings 'uninitialized';

my $Progrm = 'janein';
my $ProgrmFancy = 'JaNein';
my $CurVer = '2022-04-13';

sub Usage {
	print(qq{Usage: $Progrm [OPTS] [QUESTION]

		  -h, --help               - Display this help information.
		  -v, --version            - Output the version datestamp.
		  -N, --no-cmd [CMD]       - Run shell CMD when result is False.
		  -S, --scheme [INT]       - See below for valid button schemes.
		  -Y, --yes-cmd [CMD]      - Run shell CMD when result is True.
		  -f, --frame              - Display a frame around the widgets.
		  -q, --quiet              - Omit returned True/False booleans.

		  The use of '--' is supported in $ProgrmFancy.

		Button Schemes:

		  0: '_Yes' & '_No'               1: '_Accept' & '_Decline'
		  2: '_Agree' & '_Disagree'       3: '_Allow' & '_Deny'
		  4: '_True' & '_False'           5: '_Correct' & '_Incorrect'

		  The underscore ('_') denotes the mnemonic.
	} =~ tr/\t//dr);
}

my $Hush = 'False';
my $DoFrame = 'False';
my $Scheme = 0;

my ($YesText, $NoText, $YesCMD, $NoCMD, @ButtonSize);

while ( defined($ARGV[0]) ) {
	if ($ARGV[0] =~ '^--$') {
		shift(); last
	} elsif ($ARGV[0] =~ '^(--help|-h)$') {
		Usage(); exit(0)
	} elsif ($ARGV[0] =~ '^(--version|-v)$') {
		print("$CurVer\n"); exit(0)
	} elsif ($ARGV[0] =~ '^(--no-cmd|-N)$') {
		shift(); $NoCMD = $ARGV[0];

		die('Negative command required') unless length($NoCMD) > 0
	} elsif ($ARGV[0] =~ '^(--scheme|-S)$') {
		shift(); $Scheme = $ARGV[0];

		die("Scheme '$Scheme' not an integer") unless $Scheme =~ /^[0-9]{1}$/
	} elsif ($ARGV[0] =~ '^(--yes-cmd|-Y)$') {
		shift(); $YesCMD = $ARGV[0];

		die('Affirmative command required') unless length($YesCMD) > 0
	} elsif ($ARGV[0] =~ '^(--frame|-f)$') {
		$DoFrame = 'True'
	} elsif ($ARGV[0] =~ '^(--quiet|-q)$') {
		$Hush = 'True'
	} elsif ($ARGV[0] =~ '^-') {
		die("Option '$ARGV[0]' invalid")
	} else {
		last
	}

	shift()
}

# Remaining arguments (not options -- duh) become the question; `echo` style.
my $Question = join(' ', @ARGV);

$Question || die("Question string missing");
length($Question) > 100 && die("Question string exceeds maximum length");

if ($Scheme == 0) {
	$YesText = '_Yes';
	$NoText = '_No';
	@ButtonSize = (100, 60)
} elsif ($Scheme == 1) {
	$YesText = '_Accept';
	$NoText = '_Decline';
	@ButtonSize = (120, 60)
} elsif ($Scheme == 2) {
	$YesText = '_Agree';
	$NoText = '_Disagree';
	@ButtonSize = (120, 60)
} elsif ($Scheme == 3) {
	$YesText = '_Allow';
	$NoText = '_Deny';
	@ButtonSize = (110, 60)
} elsif ($Scheme == 4) {
	$YesText = '_True';
	$NoText = '_False';
	@ButtonSize = (120, 60)
} elsif ($Scheme == 5) {
	$YesText = '_Correct';
	$NoText = '_Incorrect';
	@ButtonSize = (120, 60)
}

sub Clicked {
	# Dereferencing the argument array sent to callback.
	my ($ExitStatus, $String) = @{$_[1]};

	print("$String\n") unless $Hush eq 'True';
	Gtk2::main_quit();

	system($YesCMD) if $String eq 'True';
	system($NoCMD) if $String eq 'False';

	exit($ExitStatus)
}

my $RootWin = Gtk2::Window->new();
	$RootWin->signal_connect('destroy' => sub {Gtk2::main_quit()});
	$RootWin->signal_connect('delete_event' => sub {return(0)});

	$RootWin->set_title($ProgrmFancy);
	$RootWin->set_default_size(280, 120);
	$RootWin->set_resizable(FALSE);
	$RootWin->set_decorated(TRUE);

my $VBox = Gtk2::VBox->new(FALSE, 4);
	$VBox->set_border_width(8);

# Putting HBoxButtons within HBoxMain to keep buttons centered sans stretching.
my $HBoxMain = Gtk2::HBox->new(FALSE, 4);
	# Ensures minimum size is maintained, else buttons would be cropped.
	$HBoxMain->set_size_request(280, 46);

my $HBoxButtons = Gtk2::HBox->new(FALSE, 4);
	$HBoxButtons->set_border_width(8);

my $Frame = Gtk2::Frame->new();
	$Frame->set_border_width(20);

my $Label = Gtk2::Label->new();
	$Label->set_text($Question);
	$Label->set_max_width_chars(100);
	$Label->set_justify('center');
	$Label->set_line_wrap(FALSE);
	$Label->set_line_wrap_mode('word');

my $JaButton = Gtk2::Button->new($YesText);
	$JaButton->set_size_request(@ButtonSize);
	$JaButton->signal_connect('clicked' => \&Clicked, [0, 'True']);

my $NeinButton = Gtk2::Button->new($NoText);
	$NeinButton->set_size_request(@ButtonSize);
	$NeinButton->signal_connect('clicked' => \&Clicked, [1, 'False']);

$VBox->pack_start($Label, TRUE, TRUE, 4);
$VBox->pack_start($HBoxMain, FALSE, FALSE, 4);

$HBoxMain->pack_start($HBoxButtons, TRUE, FALSE, 4);

$HBoxButtons->pack_start($JaButton, TRUE, TRUE, 4);
$HBoxButtons->pack_start($NeinButton, TRUE, TRUE, 4);

if ($DoFrame eq 'True') {
	$RootWin->set_border_width(8);
	$Frame->add($VBox);
	$RootWin->add($Frame);
} else {
	$RootWin->set_border_width(0);
	$RootWin->add($VBox);
}

$RootWin->show_all();

Gtk2::main($ProgrmFancy)
